plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'datafusion.java-conventions'
    id 'com.diffplug.spotless'
    id 'com.google.osdetector'
}

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'org.apache.arrow:arrow-format:9.0.0'
    implementation 'org.apache.arrow:arrow-vector:9.0.0'
    runtimeOnly 'org.apache.arrow:arrow-memory-unsafe:9.0.0'
}

spotless {
    java {
        googleJavaFormat()
    }
}

java {
    withJavadocJar()
    withSourcesJar()

    compileJava {
        options.compilerArgs += ["-h", "${buildDir}/target/headers"]
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

def cargoBinary = "${System.getProperty('user.home')}/.cargo/bin/cargo"

tasks.register('cargoDevBuild', Exec) {
    workingDir "$rootDir/datafusion-jni"
    executable cargoBinary
    args += ['build']
}

tasks.register('cargoReleaseBuild', Exec) {
    workingDir "$rootDir/datafusion-jni"
    executable cargoBinary
    args += ['build', '--release']
}

def extensionMapping = [
        "osx"    : "dylib",
        "linux"  : "so",
        "windows": "dll"
]

tasks.register('copyDevLibrary', Sync) {
    def extension = extensionMapping[osdetector.os]
    from "${rootDir}/datafusion-jni/target/debug/libdatafusion_jni.$extension"
    into layout.buildDirectory.dir("jni_libs/dev")
    dependsOn cargoDevBuild
}

tasks.register('copyBuiltLibrary', Copy) {
    def extension = extensionMapping[osdetector.os]
    from "${rootDir}/datafusion-jni/target/release/libdatafusion_jni.$extension"
    into layout.buildDirectory.dir("jni_libs/${osdetector.classifier}")
    dependsOn cargoReleaseBuild
}

def classifierOsx = 'osx-x86_64'
def extensionOsx = 'dylib'
def jniLibOsx = layout.buildDirectory.file("jni_libs/$classifierOsx/libdatafusion_jni.$extensionOsx")

def classifierLinux = 'linux-x86_64'
def extensionLinux = 'so'
def jniLibLinux = layout.buildDirectory.file("jni_libs/$classifierLinux/libdatafusion_jni.$extensionLinux")

def classifierWindows = "windows-x86_64"
def extensionWindows = "dll"
def jniLibWindows = layout.buildDirectory.file("jni_libs/$classifierWindows/datafusion_jni.$extensionWindows")

tasks.register('jarWithOsxLib', Jar) {
    from sourceSets.main.output
    from jniLibOsx
    rename "libdatafusion_jni.$extensionOsx", "jni_libs/libdatafusion_jni.$extensionOsx"
    archiveClassifier.set(classifierOsx)
}

tasks.register('jarWithLinuxLib', Jar) {
    from sourceSets.main.output
    from jniLibLinux
    rename "libdatafusion_jni.$extensionLinux", "jni_libs/libdatafusion_jni.$extensionLinux"
    archiveClassifier.set(classifierLinux)
}

tasks.register('jarWithWindowsLib', Jar) {
    from sourceSets.main.output
    from jniLibWindows
    rename "datafusion_jni.$extensionWindows", "jni_libs/datafusion_jni.$extensionWindows"
    archiveClassifier.set(classifierWindows)
}

tasks.register('jarWithLib', Jar) {
    from sourceSets.main.output
    from jniLibOsx
    rename "libdatafusion_jni.$extensionOsx", "jni_libs/libdatafusion_jni.$extensionOsx"
    from jniLibLinux
    rename "libdatafusion_jni.$extensionLinux", "jni_libs/libdatafusion_jni.$extensionLinux"
    from jniLibWindows
    rename "datafusion_jni.$extensionWindows", "jni_libs/datafusion_jni.$extensionWindows"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId 'datafusion-java'
            artifact sourcesJar
            artifact javadocJar
            artifact jarWithLib
            pom {
                name = 'DataFusion Java'
                description = 'A Java binding to Apache Arrow DataFusion library'
                url = 'https://github.com/G-Research/datafusion-java'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'dev'
                        name = 'Apache Arrow Developers'
                        email = 'dev@arrow.apache.org'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:G-Research/datafusion-java.git'
                    developerConnection = 'scm:git:https://github.com/G-Research/datafusion-java.git'
                    url = 'https://github.com/G-Research/datafusion-java'
                }
            }
        }
    }
    repositories {
        maven {
            name = "Sonatype"
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = findProperty('ossrhUsername') ?: System.getenv("MAVEN_USERNAME")
                password = findProperty('ossrhPassword') ?: System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

def artifacts = publishing.publications.mavenJava.artifacts

if (jniLibLinux.get().asFile.exists()) {
    artifacts.artifact jarWithLinuxLib
}

if (jniLibOsx.get().asFile.exists()) {
    artifacts.artifact jarWithOsxLib
}

if (jniLibWindows.get().asFile.exists()) {
    artifacts.artifact jarWithWindowsLib
}


signing {
    required { gradle.taskGraph.hasTask("publish") }
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
